      SUBROUTINE ANRT(M,N,S,EC,XMN,IEB,IEC)
C       THIS SUBROUTINE IS USED BY V071-V076
C
C         (AN)NULAR FUNCTION (R)OO(T)
C
C SOFTWARE ID : V070
C
C
C AUTHOR :    UNKNOWN
C
C DATE : UNKNOWN
C
C
C DATED REVISIONS :
C
C  11/17/87 S. L. SKILES ADDED ERROR CHECK FOR CALCULATION OF CRITICAL
C          MACH NUMBER (XMN) NOT CONVERGING BY CALLING ERRMSG(1) WHEN
C           IEC = 1
C
C 11/17/87 S. L. SKILES ADDED ERROR CHECK FOR ARGUMENT OF J BESSEL
C          FUNCTION BEING NEGATIVE BY CALLING ERRMSG(4) WHEN
C          IER = 2
C
C  11/17/87 S. L. SKILES ADDED ERROR CHECK FOR J BESSEL ACCURACY NOT
C           BEING OBTAINED IN THE CALCULATION OF CRITICAL MACH NUMBER
C           BY CALLING ERRMSG(5) WHEN IE1,IE2,IE3, OR IE4 = 3 OR 4
C
C 01/06/88 S. L. SKILES ADDED ERROR CHECK FOR J BESSEL SET TO INFINITY
C          IN THE CALCULATION OF CRITICAL MACH NUMBER BY CALLING
C          ERRMSG(6) WHEN IE5,IE6,IE7,OR IE8 = 3
C
C 11/17/87 S. L. SKILES ADDED ERROR CHECK FOR ARGUMENT OF Y BESSEL
C          FUNCTION BEING NEGATIVE OR ZERO BY CALLING ERRMSG(7) WHEN
C          IER = 2
C
C 11/17/87 S. L. SKILES ADDED ERROR CHECK FOR Y BESSEL SET TO - INFINITY
C          IN THE CALCULATION OF CRITICAL MACH NUMBER BY CALLING
C          ERRMSG(8) WHEN IE5,IE6,IE7,OR IE8 = 3
C
C 8/15/91  B. E. PRUITT MODIFIED ERROR CHECK FOR CALCULATION OF CRITICAL
C          MACH NUMBER (XMN) NOT CONVERGING BY PLACING IT IN THIS 
C          SUBROUTINE.
C
C 8/15/91  B. E. PRUITT MODIFIED ERROR CHECK FOR Y BESSEL FUNCTION SET TO 
C          MINUS MACHINE INFINITY BY PLACING IT IN THIS SUBROUTINE.
C         
C$
C -------------------------------------------------------------------
C
C PURPOSE : START WITH A GUESS SOLUTION FOR XM1, AND GET A BETTER VALUE
C           FOR XM1. GUESS EACH HIGHER ORDER ROOT FROM
C           XM(N+1) = XMN+3.14159 AND THEN REFINE IT TO GET THE (M,N)
C           ROOT TO  :   F=P(JS*Y-YS*J) = 0
C
C
C FORM OF CALL:
C     CALL ANRT(M,N,S,EC,XMN,IEB,IEC)
C
C
C ARGUMENT LIST  :
C M - CIRCUMFERENTIAL MODE NUMBER (INPUT)
C N - RADIAL MODE NUMBER + 1 (INPUT)
C S - SIGMAR (INPUT)
C EC - CONVERGENCE CRITERIA TO CALCULATE XMN (INPUT)
C XMN - NON-DIMENSIONAL ROOT OF THE EIGEN VALUE EQUATION (KMMU) (OUTPUT)
C IEC - IEC = 0 - NO ERROR IN XMN CALCULATION (OUTPUT), IEC = 1 - XMN
C        CALCULATION DID NOT CONVERGE
C IEB - ARRAY OF THE BESSEL FUNCTION ERRORS IN ANFU (OUTPUT)
C
C
C INPUT: UNKNOWN
C
C
C OUTPUT: UNKNOWN
C
C
C METHOD : REFER TO C.S. VENTRES,ET AL., "TURBOFAN NOISE GENERATION",
C          NASA-CR-167951 AND NASA-CR-167952,JULY 1982
C
C
C IDIOSYNCRASIES : UNKNOWN
C
C
C -------------------------------------------------------------------
C$
C**********************************************************************
C
C     ..................................................................
C
      IMPLICIT REAL*8 (A - H , O - Z )
      INTEGER IS,IEB(4)
      REAL*8 DX,XMN,PI
      PI=4.0D+00 * DATAN(1.0D+00)

        IF(M.EQ.0.AND.N.EQ.1) GO TO 7
          XMN = M-PI
          DO 6 NN=1,N
             DX = PI/1.D+01
             IS = (-1)**(NN+1)
             X  = XMN+PI
             J  = 1
C
	     IF(X.LE.0.D0)THEN
	       F=0.D0
	       GOTO 8
	     ENDIF
             CALL ANFU(M,X,S,F,IEB)
  8          DX = -ISIG(F*IS)*DX
             TE = F
C
  1          IF (DABS(F) .LE.  EC) GO TO 3
             IF (J .GT. 100) GO TO 2
C
             X  = X+DX
             CALL ANFU(M,X,S,F,IEB)
C
             IF ((F*TE) .LT. 0.D0)DX = -DX/2.D0
             J  = J+1
             TE = F
             GO TO 1
C
  2          IEC = 1
             GO TO 5
  3          IEC = 0
  5          XMN = X
  6        CONTINUE
C
C
          MU=N-1
          IF (IEB(3) .EQ. 3.OR.IEB(4) .EQ. 3)WRITE(0,100)M,MU
 100  FORMAT(1X,//,
     &25X,'* *  N  O  T  E  :  * *',///,
     &12X,'Y BESSEL FUNCTION SET TO MINUS MACHINE INFINITY WHILE',//,
     &12X,'CALCULATING THE CRITICAL MACH NUMBER "XMN" FOR "M ,MU =',
     &12X,I4,',',1X,I4,//)
C
C
          IF (IEC .EQ. 1) WRITE(0,200)M,MU
200   FORMAT(1X,//,
     &25X,'* *  W  A  R  N  I  N  G  * *',///,
     &10X,'CALCULATION OF CRITICAL MACH NUMBER "XMN" DID NOT CONVERGE',/
     &,10X,'FOR "M ,MU ="',1X,I4,',',1X,I4,
     &//)
        RETURN
  7     CONTINUE
C
       DO 20 II=1,4
        IEB(II)=0
   20  CONTINUE
        IEC=0
        XMN=0.0D+00
        RETURN
        END
